/* 
    Ela Core module
    Created in 2011 by Basil Voronkov
*/
open CoreInternal[elalib]

/* MATH */

let abs x | x >= 0 = x;
          | else   = --x
end

let exp = CoreInternal.exp

let gcd 0 0 = fail "Core.gcd: gcd 0 0 is undefined";
        x y = gcd' (abs x) (abs y)
              where gcd' x 0 = x;
                         x y = gcd' y (x % y)
              end
end
              
let cos = CoreInternal.cos

let sin = CoreInternal.sin

let tan = CoreInternal.tan

let quot = CoreInternal.quot

let lcm _ 0 = 0;
        0 _ = 0;
        x y = abs ((x `quot` gcd x y) * y)
end

let floor = CoreInternal.floor

let ceiling = CoreInternal.ceiling

let subtract = flip (-)

let atan = CoreInternal.atan

let sqrt x = x ** 0.5 end

let truncate = CoreInternal.truncate

let log = CoreInternal.log

let even x = x % 2 == 0 end

let odd x = x % 2 <> 0 end

let maxValue x y | x >= y = x;
                 | else   = y
end

let minValue x y | x <= y = x;
                 | else   = y
end

let round = CoreInternal.round

let rnd = CoreInternal.rnd

let pi = CoreInternal.pi

let e = CoreInternal.e


/* LIST FUNCTIONS */

let !! x y = x.[y] end

let head x::_ = x;
         []   = fail "Core.head: Empty list" 
end

let tail _::xs = xs;
         []    = fail "Core.tail: Empty list"
end

let last x::[] = x;
         _::xs = last xs;
         []    = fail "Core.last: Empty list"
end

let init x::[] = [];
         x::xs = x :: init xs;
         []    = fail "Core.init: Empty list"
end

let isNil _::_ = false; [] = true end

let takeWhile f x::xs | f x  = x :: takeWhile f xs;
                      | else = nil xs;
              f []@n         = n
end

let take 0 xs    = nil xs;
         n x::xs = x :: take (n - 1) xs;
        _ []@n   = n
end

let skip 0 xs    = xs;
         n _::xs = skip (n - 1) xs;
         _ []@n  = n
end

let each f x::xs = f x $ each f xs;
         _ []    = ()
end
       
let map f x::xs = f x :: map f xs;
        _ []@n  = n
end
        
let map' f x::xs = f x :: (& map' f xs);
         _ []@n  = n
end
           
let filter p x::xs | p x  = x :: filter p xs;
                   | else = filter p xs;
           _ []@n         = n
end

let filter' p x::xs | p x  = x :: (& filter' p xs);
                    | else = filter' p xs;
            _ []@n         = n
end

let foldl f z x::xs = foldl f (f z x) xs;
          _ z []    = z
end
       
let foldr f z x::xs = f x (foldr f z xs);
          _ z []    = z
end

let fold f xs = foldr f (nil xs) xs end

let concat x::xs = x ++ concat xs; 
           []@n  = n
end

let concatMap f x::xs = f x :: concatMap f xs; 
              _ []@n  = n
end

let sum xs = sum' 0 xs
             where sum' s x::xs = sum' (s + x) xs;
                        s []    = s
             end
end

let product xs = product' 1 xs
                 where product' p x::xs = product' (p * x) xs;
                                p []    = p
                 end
end

let minimum x::xs = minimum' x xs
                    where minimum' v x::xs | x < v = minimum' x xs;
                                           | else  = minimum' v xs;
                                   v []            = v
                    end
end

let maximum x::xs = maximum' x xs
                    where maximum' v x::xs | x > v = maximum' x xs;
                                           | else  = maximum' v xs;
                                   v []            = v
                    end
end

let reverse xs = foldl (flip (::)) (nil xs) xs end
        
let zipWith z x::xs y::ys = z x y :: zipWith z xs ys;
            _ _     ys    = nil ys
end

let any f x::xs | f x  = true;
                | else = any f xs;
        _ []           = false
end
        
let all f x::xs | f x  = all f xs;
                | else = false;
        _ []           = false
end

let splitAt 0 xs            = (nil xs,xs);
            _ []@n          = (n,n);
            n x::xs | n > 0 = (x::xs',xs2')
                              where (xs',xs2') = splitAt (n-1) xs end;
            _ _             = fail "Core.splitAt: negative argument"
end

let span p []@n = (n,n);
         p (x::xs')@xs | p x  = (x::ys, zs);
                       | else = (nil xs,xs)
                       where (ys,zs) = span p xs' end
end

let breakList p xs = span p' xs
                     where p' x = not (p x) end
end

let drop 0 xs            = xs;
         _ []@n          = n;
        n _::xs | n > 0  = drop (n-1) xs;
         _ _             = fail "Core.drop: negative argument"
end

let dropWhile p []@n = n;
              p x::xs | p x  = dropWhile p xs;
                      | else = x::xs
end

let index e lst = index' 0 lst
                  where index' n x::xs | x <> e = index' (n+1) xs;
                                       | else   = n;
                               n []    = n
                  end
end

let elem x = any (==x) end

let elemBy _  _ []    = false;
           eq y x::xs = y `eq` x || elemBy eq y xs
end

let notElem x = all (<>x) end

let scanl f q x::xs = scanl f (f q x) xs;
          _ _ []@n  = []
end

let scanr f q0 []@n  = q0::n;
          f q0 x::xs = f x q :: qs
                       where (q::_)@qs = scanr f q0 xs end
end

let repeat x = x :: (& repeat x) end

let cycle head::tail = head :: (& cycle' tail)
                       where cycle' x::xs = x :: (& cycle' xs);
                                    []    = head :: (& cycle' tail)
                       end
end

let iterate f x = x :: (& iterate f (f x)) end

let replicate n x = take n (repeat x) end

let deleteBy p x y::ys | p x y  = ys;
                       | else   = y :: deleteBy p x ys;
             _ _ []@n           = n
end

let delete = deleteBy (==)

let nubBy eq xs = nubBy' xs (nil xs)
    where nubBy' []@n _ = n;
                 y::ys xs | elemBy eq y xs = nubBy' ys xs;
                          | else           = y :: nubBy' ys (y::xs)
    end
end

let nub = nubBy (==)


/* TUPLE FUNCTIONS */

let => x y = (x, y) end

let fst (x, _) = x end

let snd (_, y) = y end

let fst3 (x, _, _) = x end

let snd3 (_, y, _) = y end

let swap (x, y) = (y, x) end

let swap3 (x, y, z) = (y, x, z) end

/* currying and uncurrying for functions with pairs and
    with triples. */
let curry f x y = f (x, y) end

let uncurry f (x, y) = f x y end

let curry3 f x y z = f (x, y, z) end

let uncurry3 f (x, y, z) = f x y z end


/* COMBINATORS */

/* id, const and sub are SKI combinators */
let id x = x end//I

let const x _ = x end//K

let sub x y z = x z (y z) end//S

/* fixed point combinator with non strict behavior;
    strict version of this function is as follows:
    let fix f x = f (fix f) x */
let fix f = f (& fix f) end

let on cmb f x y = f x `cmb` f y end


/* MISC */

/* a function with a recursive definition that can be
    evaluated solely for the side effects of it first argument.
    rec can be used for the function 'f' of one argument, rec2 - 
    for the function 'f' of two arguments. */
let rec f x = f x $ rec end

let rec2 f x y = f x y $ rec2 end

let until p f x | p x  = x;
                | else = until p f (f x)
end

let compare x y | x == y = `EQ;
                | x <= y = `LT;
                | else   = `GT
end

let chr (?int)@x = x:char end

let ord (?char)@x = x:int end
