/* 
  Module format
  Created in 2012 by Basil Voronkov
*/
open console
import libFormat#elalib@fm lang

/* Classes and instances */
class Format a where
  fmt _->a->_

instance Format Int Long Single Double String Char Bool where
  fmt f x = lang.toString (force f) x

/* Functions */
printf fstr = format' write f' [] (fm.countArgs f')
  where f' = force fstr

printfn fstr = format' writen f' [] (fm.countArgs f')
  where f' = force fstr

formatStr fstr = format' id f' [] (fm.countArgs f')
  where f' = force fstr

/* Privates */
format' # private 
format' fun fstr xs n 
  | n == 0 = fun (fm.format fstr fmt xs)
  | else = \x -> format' fun fstr (force x::xs) (n - 1)

