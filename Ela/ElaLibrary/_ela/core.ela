/*
  Module core
  Created in 2012 by Basil Voronkov
*/

/* Ordering */
type Ordering = EQ | LT | GT
  deriving Eq Ord Show

compare x y 
  | x == y = EQ 
  | x < y = LT 
  | else = GT

maxValue x y 
  | x >= y = x 
  | else   = y

minValue x y 
  | x <= y = x 
  | else   = y

/* Maybe */
type Maybe = Some a | None
  deriving Eq Show

maybe n _ None = n
maybe _ f (Some x) = f x

isSome None = false
isSome _    = true

fromSome (Some a) = a

fromMaybe d None = d
fromMaybe _ (Some x) = x

/* Either */
type Either = Left a | Right a
  deriving Eq Show

either f _ (Left x) = f x
either _ g (Right y) = g y

lefts xs = [a \\ Left a <- xs]

rights xs = [a \\ Right a <- xs]

fromLeft (Left a) = a

fromRight (Right a) = a

